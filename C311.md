# C311 零星记录

### A1

+ cartesian-product

> The cartesian-product is defined over a list of sets (again simply lists that by our agreed upon convention don't have duplicates). The result is a list of tuples (i.e. lists). Each tuple has in the first position an element of the first set, in the second position an element of the second set, etc. The output list should contains all such combinations. The exact order of your tuples may differ; this is acceptable.

输入是一个任意长度的集合列表，输出是这些集合元素的一个组合。

```scheme
(define cartesian-product
  (lambda (sets)
    (define f
      (lambda (s acc)
        (append-map (lambda (ss)
                      (map (lambda (a) (cons ss a)) acc))
             s)))
    (foldr f '(()) sets)))
```

Example

```scheme
(display (cartesian-product '()))
; (())
(display (cartesian-product '((1 2 3))))
; ((1) (2) (3))
(display (cartesian-product '((1 2 3) (a b))))
; ((1 a) (1 b) (2 a) (2 b) (3 a) (3 b))
(display (cartesian-product '((1 2 3) (a b) (H))))
; ((1 a H) (1 b H) (2 a H) (2 b H) (3 a H) (3 b H))
```

推导思路：

+ 输入为空时，结果为 '(())
+ 输入为仅有 1 个集合，应该输出它的各个元素。((a b c) => ((a) (b) (c))
+ 输入为有 2 个集合，其实是把第二个集合的元素添加到仅有一个集合的输出结果上。((a b c) (e)) => ((a e) (b e) (c e))

观察到，输入仅有 1 个集合，可以视为把这个集合的元素添加到输入为空的结果上 ((a b c)) => ((a '()) (b '()) (c '()))

需要遍历所有的集合，foldr 可以做。foldr 的 f 有两个参数，一个是当前被遍历的集合 s，一个当前累计的结果 acc。

举例来说，对于 ((a b c) (e))。第一次遍历时，`s = (a b c)， acc = '(())`；第二次遍历时，`s = (e), acc = ((a) (b) (c))`。依此类推

剩下的内容容易懂。


### A2

+ lex

> Define and test a procedure lex that takes a lambda-calculus expression and an accumulator (which starts as the empty list), and returns the same expression with all bound variable references replaced by lists of two elements whose car is the symbol var and whose cadr is the lexical address of the referenced variable. You should leave free variables as is.

```scheme
(define lex
  (lambda (e bound)
    (match e
      [`(lambda (,x) ,body) `(lambda ,[lex body (cons x bound)])]
      [`(,rator ,rand) `(,[lex rator bound] ,[lex rand bound])]
      [`,y (if (not (memv y bound)) y `(var ,[index-of bound y]))])))
```

bound 实际上充当了 env 的角色。函数入口带一个空表是出题者的提示。

```scheme
(display (lex '(lambda (y) (lambda (x) y)) '()))
; (lambda (lambda (var 1)))
(display (lex '(lambda (y) (lambda (x) (x y))) '()))
; (lambda (lambda ((var 0) (var 1))))
(display (lex '(lambda (x) (lambda (x) (x x))) '()))
; (lambda (lambda ((var 0) (var 0))))
(display (lex '(lambda (x) (lambda (x) (y x))) '()))
; (lambda (lambda (y (var 0))))
(display (lex '(lambda (y) ((lambda (x) (x y)) (lambda (c) (lambda (d) (y c))))) '()))
; (lambda ((lambda ((var 0) (var 1))) (lambda (lambda ((var 2) (var 1))))))
```